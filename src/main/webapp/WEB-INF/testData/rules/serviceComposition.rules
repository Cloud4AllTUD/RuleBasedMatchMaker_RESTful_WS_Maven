@prefix c4a: <http://rbmm.org/schemas/cloud4all/0.1/>.
@prefix rdfs: <http://www.w3.org/2000/01/rdf-schema#>.

	// Trigger for Matching a Service: noValue(?pref c4a:status "matched") 
	// TODO: matches all services, may trigger multiple service conflicts ???
	[MatchSingleService:
	(?if rdf:type c4a:InferredConfiguration)
	(?if c4a:hasPrefs ?pref)
	  
	(?pref rdf:type c4a:Preference)
	(?pref c4a:id ?p_id)
	(?pref c4a:value ?p_value)
	noValue(?pref c4a:status "matched")
	
	(?ssynth rdf:type c4a:InstalledSolution)
	(?ssynth c4a:id "http://registry.gpii.net/applications/com.certh.service-synthesis")
	(?ssynth c4a:service ?service)
	
	(?service c4a:refersTo ?p_id)
	(?service c4a:id ?s_id)
	(?service c4a:properties ?prop)
	(?prop c4a:name ?prop_name)
	(?prop c4a:value ?prop_value)
	
	makeSkolem(?newConfig, ?pref, ?if)
	makeSkolem(?newServiceSetting, ?service)
	makeSkolem(?newSetting, ?prop)
	->
	(?if c4a:refersTo ?newConfig)
	(?newConfig rdf:type c4a:Configuration)
	(?newConfig c4a:refersTo ?ssynth)
	(?newConfig c4a:refersTo ?pref)
	(?newConfig c4a:id "http://registry.gpii.net/applications/com.certh.service-synthesis")
	(?newConfig c4a:name "com.certh.service-synthesis")
	(?newConfig c4a:isActive "true")
	(?newConfig c4a:settings ?newServiceSetting)
	
	(?newServiceSetting rdf:type c4a:ServiceSetting)
	(?newServiceSetting c4a:serviceName ?s_id)
	(?newServiceSetting c4a:serviceInput ?newSetting)
	
	(?newSetting rdf:type c4a:Setting)
	(?newSetting c4a:name ?prop_name)
	(?newSetting c4a:value ?prop_value)
	]
	
	
	// Replace PREFERENCE_VALUE_TO_BE_REPLACED with the current preference value
	[ReplaceSingleServicePrefValue:
		(?if c4a:refersTo ?config)
		(?config rdf:type c4a:Configuration)
		(?config c4a:refersTo ?pref)
		(?config c4a:settings ?serviceSetting)
		(?pref rdf:type c4a:Preference)
		(?pref c4a:value ?p_val)
		(?serviceSetting rdf:type c4a:ServiceSetting)
		(?serviceSetting c4a:serviceInput ?setting)
		(?setting rdf:type c4a:Setting)
		(?setting c4a:value ?s_val)
		equal(?s_val, "PREFERENCE_VALUE_TO_BE_REPLACED")
		->
		drop(9)
		(?setting c4a:value ?p_val) 
	]